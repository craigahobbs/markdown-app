# Licensed under the MIT License
# https://github.com/craigahobbs/markdown-up/blob/main/LICENSE


#
# The MarkdownUp launcher index application
#


async function markdownUpIndex()
    # Fetch the index files/directories/parent API
    index_url = 'markdown_up_index' + if(vPath != null, '?path=' + encodeURIComponent(vPath), '')
    index = fetch(index_url)

    # Set the document title
    path = objectGet(index, 'path')
    title = 'MarkdownUp - ' + path
    setDocumentTitle(title)

    # Menu and title
    parent = objectGet(index, 'parent')
    markdownPrint( \
        if(vPath == null, 'Root', '[Root](#var=)') + ' |', \
        if(vPath == null, 'Parent', \
            '[Parent](' + if(parent == null, '#var=', "#var.vPath='" + encodeURIComponent(parent) + "'") + ')') + ' |', \
        '[MarkdownUp](https://github.com/craigahobbs/markdown-up#readme)' \
    )
    markdownPrint('# ' + title)

    # Render file links
    files = objectGet(index, 'files')
    jumpif (files == null) filesDone
        markdownPrint('', '## Files')
        ixFile = 0
        fileLoop:
            jumpif (ixFile >= arrayLength(files)) filesDone
            file = arrayGet(files, ixFile)
            fileURL = if(vPath != null, vPath + '/', '') + file
            markdownPrint('', '[' + markdownEscape(file) + '](#url=' + encodeURIComponent(fileURL) + '&var=)')
            ixFile = ixFile + 1
        jump fileLoop
    filesDone:

    # Render directory links
    directories = objectGet(index, 'directories')
    jumpif (directories == null) directoriesDone
        markdownPrint('', '## Directories')
        ixDirectory = 0
        directoryLoop:
            jumpif (ixDirectory >= arrayLength(directories)) directoriesDone
            directory = arrayGet(directories, ixDirectory)
            directoryURL = if(vPath != null, vPath + '/', '') + directory
            markdownPrint('', '[' + markdownEscape(directory) + "](#var.vPath='" + encodeURIComponent(directoryURL) + "')")
            ixDirectory = ixDirectory + 1
        jump directoryLoop
    directoriesDone:

    # Empty path?
    if(files == null && directories == null, markdownPrint('', 'No Markdown files or sub-directories found'))
endfunction
