# Licensed under the MIT License
# https://github.com/craigahobbs/markdown-up/blob/main/LICENSE


# Include sentinel
if getGlobal('unittestTests') then
    return
endif


# Test statistics
unittestTests = objectNew()
unittestWarnings = arrayNew()


# $function: unittestRunTest
# $group: unittest
# $doc: Run a unit test
# $arg testName: The test function name
function unittestRunTest(testName)
    testFn = unittestRunTestHelper(testName)
    if testFn then
        testFn()
        setGlobal('unittestTestName', null)
    endif
endfunction


# $function: unittestRunTestAsync
# $group: unittest
# $doc: Run an asyncronous unit test
# $arg testName: The test function name
async function unittestRunTestAsync(testName)
    testFn = unittestRunTestHelper(testName)
    if testFn then
        testFn()
        setGlobal('unittestTestName', null)
    endif
endfunction


# unittestRunTest helper function
function unittestRunTestHelper(testName)
    # Single test argument?
    if vTest != null && vTest != testName then
        return null
    endif

    # Test run multiple times?
    if objectHas(unittestTests, testName) then
        arrayPush(unittestWarnings, 'Test "' + testName + '" run multiple times')
        return null
    endif

    # Get the test func
    testFn = getGlobal(testName)
    if testFn == null then
        arrayPush(unittestWarnings, 'Test "' + testName + '" not found')
        return null
    endif

    # Add the unit test result array
    testFailures = arrayNew()
    objectSet(unittestTests, testName, testFailures)
    setGlobal('unittestTestName', testName)

    return testFn
endfunction


# $function: unittestReport
# $group: unittest
# $doc: Render the unit test report
function unittestReport()
    # Compute test statistics
    testNames = arraySort(objectKeys(unittestTests))
    testCount = arrayLength(testNames)
    testFailCount = 0
    foreach testName in testNames do
        testFailures = objectGet(getGlobal('unittestTests'), testName)
        if arrayLength(testFailures) then
            testFailCount = testFailCount + 1
        endif
    endforeach
    testPassCount = testCount - testFailCount

    # Report statistics
    markdownPrint( \
        'Ran ' + testCount + ' tests - ' + testPassCount + ' passed, ' + testFailCount + ' failed' \
    )

    # Report any warnings
    if arrayLength(unittestWarnings) then
        markdownPrint('', '## Warnings')
        foreach warning in unittestWarnings do
            markdownPrint('', '- ' + markdownEscape(warning))
        endforeach
    endif

    # Report the failing tests
    if testFailCount then
        markdownPrint('', '## Failing Tests')
        foreach testName in testNames do
            testFailures = objectGet(getGlobal('unittestTests'), testName)
            if arrayLength(testFailures) then
                markdownPrint('', '[' + markdownEscape(testName) + "](#var.vTest='" + encodeURIComponent(testName) + "') - FAIL")
                foreach error in testFailures do
                    markdownPrint('', '- ' + markdownEscape(error))
                endforeach
            endif
        endforeach
    endif

    # Report the failing tests
    if testPassCount then
        markdownPrint('', '## Passing Tests')
        foreach testName in testNames do
            testFailures = objectGet(getGlobal('unittestTests'), testName)
            if !arrayLength(testFailures) then
                markdownPrint('', '[' + markdownEscape(testName) + "](#var.vTest='" + encodeURIComponent(testName) + "') - OK")
            endif
        endforeach
    endif
endfunction


# $function: unittestReset
# $group: unittest
# $doc: Reset the unit test data
function unittestReset()
    setGlobal('unittestTests', objectNew())
    setGlobal('unittestWarnings', arrayNew())
endfunction


# $function: unittestEquals
# $group: unittest
# $doc: Assert an actual value is equal to the expected value
# $arg actual: The actual value
# $arg expected: The expected value
# $arg description: The description of the assertion
function unittestEquals(actual, expected, description)
    if actual != expected then
        unittestRecordError(jsonStringify(actual), jsonStringify(expected), description)
    endif
endfunction


# $function: unittestDeepEquals
# $group: unittest
# $doc: Assert an actual value is *deeply* equal to the expected value
# $arg actual: The actual value
# $arg expected: The expected value
# $arg description: The description of the assertion
function unittestDeepEquals(actual, expected, description)
    actualJSON = jsonStringify(actual)
    expectedJSON = jsonStringify(expected)
    if actualJSON != expectedJSON then
        unittestRecordError(actualJSON, expectedJSON, description)
    endif
endfunction


# Helper function to record a unit test error
function unittestRecordError(actualStr, expectedStr, description)
    testName = getGlobal('unittestTestName')
    testFailures = objectGet(getGlobal('unittestTests'), testName)
    arrayPush(testFailures, if(description != null, description + ' - ', '') + actualStr + ' != ' + expectedStr)
endfunction
